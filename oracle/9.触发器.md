# 触发器

当用户执行了 insert | update | delete 这些操作之后, 可以触发一系列其它的动作/业务逻辑。
## 作用 
 1.在动作执行之前或者之后,触发业务处理逻辑；
 2.插入数据,做一些校验；

## 语法

```sql
create [or replace] trigger 触发器的名称
before | after
insert | update | delete 
on 表名
[for each row]
declare
           
begin
             
end;
```

## 触发器的分类

* 语句级触发器：不管影响多少行, 都只会执行一次；
* 行级触发器：影响多少行,就触发多少次
   :old  代表旧的记录, 更新前的记录
   :new  代表的是新的记录

### 语句级触发器

* 新员工入职之后,输出一句话: 欢迎你！

  ```sql
  create or replace trigger tri_test1
  after
  insert
  on emp
  declare
  
  begin
    dbms_output.put_line('欢迎加入黑马程序员');
  end;
  ```

* 数据校验, 星期六老板不在, 不能办理新员工入职
  --在插入数据之前
  --判断当前日期是否是周六
  --如果是周六,就不能插入

  ```sql
  create or replace trigger tri_test2
  before
  insert 
  on emp
  declare
   --声明变量
   vday varchar2(10);
  begin
    --查询当前
    select trim(to_char(sysdate,'day')) into vday from dual;
    --判断当前日期:
    if vday = 'saturday' then
       dbms_output.put_line('老板不在,不能办理入职');
       --抛出系统异常
       raise_application_error(-20001,'老板不在,不能办理入职');
    end if;
  end;
  ```

### 行级触发器

* 更新所有的工资 输出一句话

  ```sql
  create or replace trigger tri_test3
  after
  update
  on emp 
  for each row
  declare
  
  begin
    dbms_output.put_line('更新了数据');
  end;
  ```

  结果：由于更新了所有的工资，即有多少行记录就输出多少行“更新了数据”；

* 判断员工涨工资后的工资一定要大于涨工资前的工资

  ```sql
  /*
     200 --> 100
     触发器 : before
        旧的工资 
        新的工资
        如果旧的工资大于新的工资 , 抛出异常,不让它执行成功   
        
        
     触发器中不能提交事务,也不能回滚事务 
  */
  create or replace trigger tri_updatesal
  before
  update
  on emp
  for each row
  declare
  
  begin
    if :old.sal > :new.sal then
      raise_application_error(-20002,'旧的工资不能大于新的工资');
    end if;
  end;
  ```

* 模拟mysql中ID的自增属性 auto_increment 

  insert into person(null,'张三');  --在oracle中这样是错误，可以通过序列和触发器，在插入之前，先给pid插入值；

  ```sql
  create table person(
      pid number primary key,
      pname varchar2(20)   
  );
  --此时insert into person values(null,'张三'); 是有误的，因为不能令主键为null；
  
  --序列
  create sequence seq_person_pid;
  
  --触发器
  create or replace trigger tri_add_person_pid
  before
  insert
  on person
  for each row
  declare
  
  begin
    dbms_output.put_line(:new.pname);
    --给新记录 pid 赋值
    select seq_person_pid.nextval into :new.pid from dual;
  end;
  ```

  



