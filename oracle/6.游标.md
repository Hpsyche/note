# 游标

游标:用来操作查询的结果集，相当于JDBC中的ResultSet

## 语法

语法: cursor 游标名[(参数名 参数类型)] is 查询结果集

开发步骤：
              1.  声明游标 ：  
              2.  打开游标 :  open 游标名 
              3.  从游标中取数据  fetch 游标名 into 变量（从 游标名 中取数据，塞到 变量 中）
                    游标名%found :找到数据  游标名%notfound : 没有找到数据 
              4.  关闭游标  close 游标名

* 输出员工表中所有的员工姓名和工资(不带参数游标)
  /*
     游标:所有员工
     声明一个变量,用来记录一行数据  %rowtype
  */

```sql
declare
   --游标
   cursor vrows is select * from emp;
   --s声明变量,记录一行数据
   vrow emp%rowtype;
begin
   --1.打开游标  
   open vrows;
   --2.从游标提取数据
   --循环取数据
   loop
       fetch vrows into vrow; 
       exit when vrows%notfound;  
       dbms_output.put_line('姓名:'||vrow.ename ||' 工资: ' || vrow.sal);
   end loop;
   --3.关闭游标
   close vrows;
end;
```

* 输出指定部门下的员工姓名和工资

/*
   游标: 指定部门的所有员工
   声明一个变量记录一行数据
*/

```sql
declare
   --声明游标
   cursor vrows(dno number) is select * from emp where deptno = dno;
   --声明变量
   vrow emp%rowtype;
begin
  --1.打开游标 , 指定10号部门
  open vrows(10);
  --2. 循环遍历,取数据
  loop
     fetch vrows into vrow;
     exit when vrows%notfound;    
      dbms_output.put_line('姓名:'||vrow.ename ||' 工资: ' || vrow.sal);
  end loop;
  close vrows;
end;
```

## 系统引用游标

1. 声明游标 : 游标名 sys_refcursor
2. 打开游标: open 游标名 for 结果集
3. 从游标中取数据
4. 关闭游标

* 系统引用游标
  --输出员工表中所有的员工姓名和工资

  ```sql
  declare
    --声明系统引用游标
    vrows sys_refcursor;
    --声明一个变量
    vrow emp%rowtype;
  begin
    --1.打开游标
    open vrows for select * from emp;
    --2.取数据
    loop
      fetch vrows into vrow;
      exit when vrows%notfound;
       dbms_output.put_line('姓名:'||vrow.ename ||' 工资: ' || vrow.sal);
    end loop;
    close vrows;
  end;
  ```

## for循环遍历游标

* 扩展内容----使用for循环遍历游标

  for循环遍历游标:
             不需要声明额外变量
             不需要打开游标
             不需要关闭游标 

  ```sql
  declare
    --声明一个游标
    cursor vrows is select * from emp;
  begin
    for vrow in vrows loop
       dbms_output.put_line('姓名:'||vrow.ename ||' 工资: ' || vrow.sal || '工作:'|| vrow.job);
    end loop;
  end;
  ```

* 按照员工工作给所有员工涨工资,总裁涨1000,经理涨800,其他人涨400
  /*
      游标 : 所有员工
      声明一个记录一行数据
  */

  

  ```sql
  declare
     --声明游标
     cursor vrows is select * from emp;
     --声明一个变量
     vrow emp%rowtype; 
  begin
    --1.打开游标
    open vrows;
    --2.循环取数据
    loop
         --取数据
         fetch vrows into vrow;
         --退出条件
         exit when vrows%notfound;  
         --根据不同的职位,涨工资 总裁涨1000,经理涨800,其他人涨400
         if vrow.job = 'PRESIDENT' then
            update emp set sal = sal + 1000 where empno = vrow.empno;
         elsif vrow.job = 'MANAGER' then
            update emp set sal = sal + 800 where empno = vrow.empno;
         else
            update emp set sal = sal + 400 where empno = vrow.empno; 
         end if;       
    end loop;
    --3.关闭游标
    close vrows;
    --4.提交事务
    commit;
  end;
  ```



# 例外

例外:(意外)程序运行的过程发生异常,相当于是JAVA中的异常

```sql
declare
       --声明变量
   begin
       --业务逻辑
   exception
       --处理异常
       when 异常1 then
         ...
       when 异常2 then
         ...
       when others then
         ...处理其它异常
   end;
   
   --常见异常
   zero_divide : 除零异常
   value_error : 类型转换异常
   too_many_rows : 查询出多行记录,但是赋值给了rowtype记录一行数据变量
   no_data_found : 没有找到数据
   
   自定义异常:
       异常名  exception;
       raise 异常名 
```

```sql
declare
   vi number;
   vrow emp%rowtype;
begin
   --vi := 8/0;  
   --vi := 'aaa';
   --select * into vrow from emp;
   select * into vrow from emp where empno=1234567;
exception
  when zero_divide then
    dbms_output.put_line('发生了除零异常');
  when value_error then
     dbms_output.put_line('发生了类型转换异常');
  when too_many_rows then
    dbms_output.put_line(' 查询出多行记录,但是赋值给了rowtype记录一行数据变量');
  when no_data_found then
    dbms_output.put_line('没有找到数据异常');
  when others then
     dbms_output.put_line('发生了其它异常' || sqlerrm);     
end;
```

自定义异常:
       异常名  exception;
       raise 异常名          

* 查询指定编号的员工,如果没有找到,则抛出自定义的异常
  /*
       **<font color=red>--错误的演示</font>**
       1.声明一个变量 %rowtype
       2.查询员工信息,保存起来
       3.判断员工信息是否为空
       4.如果是 则抛出异常
  */

  ```sql
  declare
    --   1.声明一个变量 %rowtype
    vrow emp%rowtype;
    --2 .声明一个自定义的异常
    no_emp exception;  
  begin
    --查询员工信息,保存起来
    select * into vrow from emp where empno = 8888;   --抛出异常
    
    if vrow.sal is null then
      raise no_emp; --抛出自定义的异常
    end if;
  exception
    when no_emp then
       dbms_output.put_line('输出了自定义的异常');  
    when others then
       dbms_output.put_line('输出了其它异常'||sqlerrm);  
  end;
  ```

* 查询指定编号的员工,如果没有找到,则抛出自定义的异常
  /*
       游标来判断
         %found %notfound
      声明一个游标
      声明一个变量,记录数据
      从游标中取记录
         如果有,则不管它
         如果没有就抛出自定义的异常
  */





